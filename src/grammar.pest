// Input
// ...

Input = _{ SOI ~ Expr* ~ EOI }

Word = _{
  ASCII_ALPHA+
}

Expr = _{
  WHITE_SPACE*
  ~ (AtTimeExpr | Word | DurationExpr)
  ~ WHITE_SPACE*
}

// Duration
// ...

Hours = {
  "hour" ~ "s"?
  | "hr" ~ "s"?
  | "h"
}

Minutes = {
  "minute" ~ "s"?
  | "min" ~ "s"?
  | "m"
}

Seconds = {
  "second" ~ "s"?
  | "sec" ~ "s"?
  | "s"
}

Duration = {
  "-"?
  ~ ASCII_DIGIT*
  ~ ("." ~ ASCII_DIGIT*)?
}

DurationExpr = {
  Duration
  ~ WHITE_SPACE*
  ~ (Hours | Minutes | Seconds)
}

// At Time
// ...

AtHours = {
  ASCII_DIGIT+
}

AtMinutes = {
  ASCII_DIGIT+
}

TimeValue = _{
  AtHours
  ~ ":"?
  ~ AtMinutes?
}

Pm = { "PM" | "pm" }
Am = { "AM" | "am" }

AtTime = {
  TimeValue
  ~ (WHITE_SPACE* ~ (Am | Pm))?
  ~ (WHITE_SPACE* ~ Timezone)?
}

AtTimeExpr = _{
  "at"
  ~ WHITE_SPACE+
  ~ AtTime
}

// Timezone
// ...

TimezoneHoursSimple = {
  ASCII_DIGIT{1,2}
}

TimezoneHours = {
  ASCII_DIGIT{2}
}

TimezoneMinutes = {
  ASCII_DIGIT{2}
}

TimezonePositive = { "+" }
TimezoneNegative = { "-" }

TimezoneLiteral = {
  ("UTC" | "utc" | "Utc")?

  ~ (TimezonePositive | TimezoneNegative)

  ~ (
    (TimezoneHoursSimple ~ (":" ~ TimezoneMinutes)?)
    | (TimezoneHours ~ TimezoneMinutes)
  )
}

Timezone = {
  TimezoneLiteral
}
